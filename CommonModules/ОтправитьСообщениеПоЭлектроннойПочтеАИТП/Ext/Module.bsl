//////////////////////////////////////////////////////////////////////////////////////////
//
// Реализация действий бизнес-процесса ОтправитьСообщениеПоЭлектроннойПочтеАИТП
//
//////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик регламентного задания действия по отправки почты
//
Процедура ОтправитьСообщенияПоЭлектроннойПочтеАИТП() Экспорт
	// Вставить содержимое обработчика.
	БизнесПроцессыАИТП.ВыполнитьДействияПоТочкеМаршрута(
		БизнесПроцессы.ОтправитьСообщениеПоЭлектроннойПочтеАИТП.ТочкиМаршрута.ОтправитьСообщение
		, "ОтправитьСообщениеПоЭлектроннойПочтеАИТП.ОбработчикВыполнитьДействиеОтправитьСообщениеПоЭлектроннойПочте"
		, // Обработчик перед выполнением действий
		, // Обработчик после выполнения действий
		, // Обработчик ошибки при выполнении действия
		, // Обработчик ошибки перед выполнением действий
		, // Обработчик ошибки после выполнения действий
		, "ОтправитьСообщениеПоЭлектроннойПочтеАИТП.ОбработчикОповещения"
		, "ОтправитьСообщениеПоЭлектроннойПочтеАИТП.ОбработчикОповещения"
		, "ОтправитьСообщениеПоЭлектроннойПочтеАИТП.ОбработчикОповещения"
	);
	
КонецПроцедуры

// Обработчик действия по отправке сообщения
//
Процедура ОбработчикВыполнитьДействиеОтправитьСообщениеПоЭлектроннойПочте(ТочкаМаршрута, Задача, ПараметрыВыполнения) Экспорт
	
	Получатели = Задача.БизнесПроцесс.Получатели;
	Получатели = Получатели.ВыгрузитьКолонку("Получатель");
	ПолучателиКопии = Задача.БизнесПроцесс.ПолучателиКопии;
	ПолучателиКопии = ПолучателиКопии.ВыгрузитьКолонку("Получатель");
	ТелоСообщения = Задача.БизнесПроцесс.Тело;
	Тема = Задача.БизнесПроцесс.Тема;
	ТипТекстаСообщения = Задача.БизнесПроцесс.ТипТекстаСообщения;
	
	ОтправкаСообщенийПоЭлектроннойПочтеВызовСервераАИТП.ОтправитьСообщение(
		Получатели
		, ТелоСообщения
		, Тема
		, ПолучателиКопии
		, ТипТекстаСообщения
	);
	
КонецПроцедуры

// Обработчик оповещения для бизнес-процесса
// Поскольку это бизнес-процесс по отправке почты - обработчик непосредственно отправляет почту
//
Процедура ОбработчикОповещения(ПараметрыОповещения) Экспорт
	
	Тема = Неопределено;
	Тело = Неопределено;
	ПолучателиОповещения = Новый Массив;
	
	ОповещениеПользователейАИТП.ОпределитьПолучателейОповещенияСтандартнаяОбработка(ПолучателиОповещения, ПараметрыОповещения);
	ОповещениеПользователейПоЭлектроннойПочтеАИТП.ЗаполнитьТемуИТелоСообщения(Тема, Тело, ПараметрыОповещения);	
	
	Если ПолучателиОповещения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Тема = Неопределено И Тело = Неопределено Тогда 
		Возврат;
	ИначеЕсли Тема = Неопределено Тогда
		Тема = "";
	ИначеЕсли Тело = Неопределено Тогда
		Тело = "";
	КонецЕсли;
	
	ОтправкаСообщенийПоЭлектроннойПочтеВызовСервераАИТП.ОтправитьСообщениеАсинхронно(
		ПолучателиОповещения
		, Тело
		, Тема
		, 
		, Константы.ТипТекстаСообщенийЭлектроннойПочтыАИТП.Получить()
	);
	
КонецПроцедуры

#КонецОбласти

