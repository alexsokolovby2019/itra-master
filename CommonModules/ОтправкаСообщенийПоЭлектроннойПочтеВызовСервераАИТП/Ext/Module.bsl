///////////////////////////////////////////////////////////////////////////
//
// Функции отправки сообщений по электронной почте
//
//////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Отправляет сообщение по электронной почте
//
// Параметры:
//  Получатели			 - Массив - список получателей электронной почты 
//  ТелоСообщения		 - Строка - тело сообщения 
//  Тема				 - Строка - тема сообщения 
//  ПолучателиКопии		 - Массив - список получателей копии письма 
//  ТипТекстаСообщения	 - Перечисление.ТипыТекстаСообщенийЭлектроннойПочты - тип тела сообщения html или текст 
//
Процедура ОтправитьСообщение(Получатели, ТелоСообщения = "", Тема = "", ПолучателиКопии = Неопределено, ТипТекстаСообщения = Неопределено) Экспорт
	
	Отправитель = Константы.СистемныйПолучательЭлектроннойПочтыАИТП.Получить();
    Сообщение = Новый ИнтернетПочтовоеСообщение;
	
	Сообщение.Отправитель.Адрес = Отправитель.АдресЭлектроннойПочты;
	Сообщение.Отправитель.ОтображаемоеИмя = Отправитель.Наименование;
	
	Сообщение.Тема = Тема;
	
	Если ТипТекстаСообщения = Перечисления.ТипыТекстаСообщенийЭлектроннойПочтыАИТП.ПустаяСсылка() 
		Или ТипТекстаСообщения = Неопределено Тогда
		
		ТипТекстаСообщения = Константы.ТипТекстаСообщенийЭлектроннойПочтыАИТП.Получить();	
		
	КонецЕсли;
	
	ТипТекстаСообщения = ПолучитьТипТекстаСообщенияИзПеречисления(ТипТекстаСообщения);
   	Сообщение.Тексты.Добавить(ТелоСообщения, ТипТекстаСообщения);
	
	ДобавитьПолучателейВСписок(Получатели, Сообщение.Получатели);
	ДобавитьПолучателейВСписок(ПолучателиКопии, Сообщение.Копии);
	
	ИПП = Новый ИнтернетПочтовыйПрофиль;
	НастройкиSMTP = Константы.СерверЭлектроннойПочтыАИТП.Получить();
	
	ИПП.АдресСервераSMTP = НастройкиSMTP.SMTPСервер.ИмяХоста;
    ИПП.ПортSMTP = НастройкиSMTP.Порт;
	
	Если Не НастройкиSMTP.УчетныеДанные = Справочники.УчетныеДанныеАИТП.ПустаяСсылка() Тогда
		
		ИПП.ПользовательSMTP = НастройкиSMTP.УчетныеДанные.Логин;
    	ИПП.ПарольSMTP = Справочники.УчетныеДанныеАИТП.ПолучитьПароль(НастройкиSMTP.УчетныеДанные.Пароль);
		
	КонецЕсли;
	
	Почта = Новый ИнтернетПочта;
    Почта.Подключиться(ИПП);
	Почта.Послать(Сообщение);
	Почта.Отключиться();
	
КонецПроцедуры

// Процедура - Отправляет сообщение асинхронно
//
// Параметры:
//  Получатели			 - Массив - список получателей электронной почты 
//  ТелоСообщения		 - Строка - тело сообщения 
//  Тема				 - Строка - тема сообщения 
//  ПолучателиКопии		 - Массив - список получателей копии сообщения 
//  ТипТекстаСообщения	 - Перечисление.ТипыТекстаСообщенийЭлектроннойПочты - тип тела сообщения. Может быть html или текст 
//
Процедура ОтправитьСообщениеАсинхронно(Получатели, ТелоСообщения = "", Тема = "", ПолучателиКопии = Неопределено, ТипТекстаСообщения = Неопределено) Экспорт
	
	Параметры = Новый Массив;
	
	Параметры.Добавить(Получатели);
	Параметры.Добавить(ТелоСообщения);
	Параметры.Добавить(Тема);
	Параметры.Добавить(ПолучателиКопии);
	Параметры.Добавить(ТипТекстаСообщения);
	
	ФоновыеЗадания.Выполнить("ОтправкаСообщенийПоЭлектроннойПочтеВызовСервераАИТП.ОтправитьСообщение"
		, Параметры
		,
		, "Отправить сообщение по электронной почте"
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Преобразует значение перечисления ТипыТекстаСообщенийЭлектроннойПочтыАИТП в ТипТекстаПочтовогоСообщения
//
Функция ПолучитьТипТекстаСообщенияИзПеречисления(ЗначениеПеречисления) Экспорт
	
	Если ЗначениеПеречисления = Перечисления.ТипыТекстаСообщенийЭлектроннойПочтыАИТП.ПростойТекст Тогда
		
		Возврат ТипТекстаПочтовогоСообщения.ПростойТекст;
		
	Иначе
		
		Возврат ТипТекстаПочтовогоСообщения.HTML;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет получателей из источника в массив СписокНазначения
//
Процедура ДобавитьПолучателейВСписок(Источник, СписокНазначения)
	
	Если ТипЗнч(Источник) = Тип("Строка") Тогда 
		
		СписокНазначения.Добавить(Источник);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.КонтактыАИТП") 
		Или ТипЗнч(Источник) = Тип("СправочникОбъект.КонтактыАИТП") Тогда
		
		СписокНазначения.Добавить(Источник.АдресЭлектроннойПочты);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.ГруппыКонтактовАИТП")
		Или ТипЗнч(Источник) = Тип("СправочникОбъект.ГруппыКонтактовАИТП") Тогда
		
		Для каждого Получатель Из Источник.Получатели Цикл
		
			Источник.Добавить(Получатель.АдресЭлектроннойПочты);	
		
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("Массив") Тогда
		
		Для каждого Получатель Из Источник Цикл
		
			ДобавитьПолучателейВСписок(Получатель, СписокНазначения);
		
		КонецЦикла; 
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти